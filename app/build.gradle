/*
 * Copyright (c) 2018. Vlad Kalyuzhnyu <vladkalyuzhnyu@gmail.com>
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("keystore/debug.jks")
            storePassword "androidovshchik"
            keyAlias "androidovshchik"
            keyPassword "androidovshchik"
            // countryCode 07
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            shrinkResources true
            minifyEnabled true
            useProguard true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$KOTLINX_COROUTINES_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$KOTLINX_COROUTINES_VERSION"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'com.google.android.gms:play-services-base:16.0.1'
    implementation 'com.marcinmoskala.PreferenceHolder:preferenceholder:1.51'
    implementation 'com.jakewharton.timber:timber:4.7.1'
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    // implementation 'com.snatik:storage:2.1.0'
}

dependencies {
    debugImplementation "com.willowtreeapps.hyperion:hyperion-core:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-attr:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-measurement:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-disk:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-shared-preferences:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-recorder:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-timber:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-phoenix:$HYPERION_VERSION"
    debugImplementation 'com.github.Commit451:Hyperion-Chuck:1.0.0'
    debugImplementation 'com.github.takahirom:Hyperion-Simple-Item:0.4.0'
    debugImplementation "ch.acra:acra-mail:$ACRA_VERSION"
    debugImplementation "ch.acra:acra-dialog:$ACRA_VERSION"
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${applicationId.substring(applicationId.lastIndexOf(".") + 1)}${variant.versionCode}-${variant.buildType.name}.apk"
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin' && requested.name == 'kotlin-reflect') {
            details.useVersion KOTLIN_VERSION
        }
    }
}