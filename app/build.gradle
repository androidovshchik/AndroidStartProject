/*
 * Copyright (c) 2019. Vlad Kalyuzhnyu <vladkalyuzhnyu@gmail.com>
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    defaultConfig {
        applicationId PACKAGE_NAME
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("keystore/debug.jks")
            storePassword "androidovshchik"
            keyAlias "androidovshchik"
            keyPassword "androidovshchik"
            // countryCode 07
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            useProguard true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$KOTLINX_COROUTINES_VERSION"
    implementation "org.jetbrains.anko:anko-commons:$ANKO_VERSION" // https://github.com/Kotlin/anko/wiki/Anko-Commons-â€“-Intents
    implementation "org.jetbrains.anko:anko-sdk19:$ANKO_VERSION" // sdk15, sdk19, sdk21, sdk23 are also available
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'com.google.android.gms:play-services-base:16.0.1'
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger-android-support:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // implementation 'com.github.bumptech.glide:glide:4.8.0'
    // kapt 'com.github.bumptech.glide:compiler:4.8.0'
    // implementation 'com.snatik:storage:2.1.0'
    implementation 'io.github.inflationx:calligraphy3:3.0.0'
    implementation 'io.github.inflationx:viewpump:1.0.0'
}

dependencies {
    debugImplementation "com.willowtreeapps.hyperion:hyperion-core:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-attr:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-measurement:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-disk:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-shared-preferences:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-recorder:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-timber:$HYPERION_VERSION"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-phoenix:$HYPERION_VERSION"
    debugImplementation 'com.github.Commit451:Hyperion-Chuck:1.0.0'
    debugImplementation 'com.github.takahirom:Hyperion-Simple-Item:0.4.0'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation "ch.acra:acra-mail:$ACRA_VERSION"
    debugImplementation "ch.acra:acra-dialog:$ACRA_VERSION"
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "${applicationId.substring(applicationId.lastIndexOf(".") + 1)}${variant.versionCode}-${variant.buildType.name}.apk"
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'org.jetbrains.kotlin' && requested.name == 'kotlin-reflect') {
            details.useVersion KOTLIN_VERSION
        }
    }
}